locals {
  config           = yamldecode(file("${path.module}/config.yml"))
  common           = local.config["common"]
  env_space        = yamldecode(file("${path.module}/config_${terraform.workspace}.yml"))
  workspace        = local.env_space["workspace"]
  workspace_common = local.workspace["common"]
  workspace_aws    = local.workspace["aws"]
  aws_region       = "ap-south-1"

  service_name_prefix = "dhk-${local.common["project_name_prefix"]}"
  common_tags = merge(
    local.common["tags"],
    local.workspace_common["tags"],
    tomap({
      "Environment" = local.workspace_common["environment"]
      "Workspace"   = terraform.workspace
    })
  )

  role_enable = local.workspace["aws"]["role"] == "" ? [] : ["arn:aws:iam::${local.workspace["aws"]["account_id"]}:role/${local.workspace["aws"]["role"]}"]

}

provider "aws" {
  region = local.aws_region
  dynamic "assume_role" {
    for_each = local.role_enable
    content {
      role_arn = assume_role.value
    }
  }
}

data "aws_caller_identity" "current" {}
